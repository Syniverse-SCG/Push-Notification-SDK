plugins {
    id "com.jfrog.bintray" version "1.7"
    id "com.github.dcendents.android-maven" version "1.5"
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'maven'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 5
        versionName "1.3.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        scg(MavenPublication) {
            groupId 'com.syniverse.scg'
            artifactId 'push-sdk'
            version android.defaultConfig.versionName
//            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/lib-release.aar")

            // Add all dependencies into POM file, so when we include the library
            // with transitive dependencies all dependencies will be resolved
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }

//    //publish to filesystem repo
//    repositories{
//        maven {
//            url "${System.env.HOME}/.m2/repository"
//        }
//    }

    repositories {
        mavenLocal()
    }
}

dependencies {
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.google.firebase:firebase-messaging:9.8.0'

    // Required for local unit tests (JUnit 4 framework)
    testCompile 'junit:junit:4.12'

    // Required for instrumented tests
    androidTestCompile 'com.android.support:support-annotations:25.3.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    // Set this dependency to use JUnit 4 rules
    androidTestCompile 'com.android.support.test:rules:0.5'
}

Properties propertiesPublish = new Properties()
propertiesPublish.load(project.rootProject.file('publish.properties').newDataInputStream())

archivesBaseName = propertiesPublish.getProperty("artifact")
group = propertiesPublish.getProperty("group")
version = propertiesPublish.getProperty("version")

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : properties.getProperty("bintrayUser")
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = propertiesPublish.getProperty("repo")
        name = propertiesPublish.getProperty("packageName")
        userOrg = propertiesPublish.getProperty("userOrg") != null ? propertiesPublish.getProperty("userOrg") : user
        licenses = [propertiesPublish.getProperty('license')]
        vcsUrl = propertiesPublish.getProperty("gitUrl")
        version {
            name = propertiesPublish.getProperty("version")
            desc = propertiesPublish.getProperty("desc")
            vcsTag = propertiesPublish.getProperty("vcsTag")
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            group propertiesPublish.getProperty("group")
            artifactId propertiesPublish.getProperty("artifact")
            version propertiesPublish.getProperty("version")
        }
    }
}
